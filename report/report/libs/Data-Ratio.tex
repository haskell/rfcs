\haddockmoduleheading{Data.Ratio}
\label{module:Data.Ratio}
\haddockbeginheader
{\haddockverb\begin{verbatim}
module Data.Ratio (
    Ratio,  Rational,  (%),  numerator,  denominator,  approxRational
  ) where\end{verbatim}}
\haddockendheader

\begin{haddockdesc}
\item[\begin{tabular}{@{}l}
data\ Integral\ a\ =>\ Ratio\ a
\end{tabular}]\haddockbegindoc
Rational numbers, with numerator and denominator of some \haddockid{Integral} type.
\par

\end{haddockdesc}
\begin{haddockdesc}
\item[\begin{tabular}{@{}l}
instance\ Integral\ a\ =>\ Enum\ (Ratio\ a)\\instance\ Integral\ a\ =>\ Eq\ (Ratio\ a)\\instance\ Integral\ a\ =>\ Fractional\ (Ratio\ a)\\instance\ Integral\ a\ =>\ Num\ (Ratio\ a)\\instance\ Integral\ a\ =>\ Ord\ (Ratio\ a)\\instance\ (Integral\ a,\ Read\ a)\ =>\ Read\ (Ratio\ a)\\instance\ Integral\ a\ =>\ Real\ (Ratio\ a)\\instance\ Integral\ a\ =>\ RealFrac\ (Ratio\ a)\\instance\ Integral\ a\ =>\ Show\ (Ratio\ a)
\end{tabular}]
\end{haddockdesc}
\begin{haddockdesc}
\item[\begin{tabular}{@{}l}
type\ Rational\ =\ Ratio\ Integer
\end{tabular}]\haddockbegindoc
Arbitrary-precision rational numbers, represented as a ratio of
 two \haddockid{Integer} values.  A rational number may be constructed using
 the \haddockid{{\char '45}} operator.
\par

\end{haddockdesc}
\begin{haddockdesc}
\item[\begin{tabular}{@{}l}
({\char '45})\ ::\ Integral\ a\ =>\ a\ ->\ a\ ->\ Ratio\ a
\end{tabular}]\haddockbegindoc
Forms the ratio of two integral numbers.
\par

\end{haddockdesc}
\begin{haddockdesc}
\item[\begin{tabular}{@{}l}
numerator\ ::\ Integral\ a\ =>\ Ratio\ a\ ->\ a
\end{tabular}]\haddockbegindoc
Extract the numerator of the ratio in reduced form:
 the numerator and denominator have no common factor and the denominator
 is positive.
\par

\end{haddockdesc}
\begin{haddockdesc}
\item[\begin{tabular}{@{}l}
denominator\ ::\ Integral\ a\ =>\ Ratio\ a\ ->\ a
\end{tabular}]\haddockbegindoc
Extract the denominator of the ratio in reduced form:
 the numerator and denominator have no common factor and the denominator
 is positive.
\par

\end{haddockdesc}
\begin{haddockdesc}
\item[\begin{tabular}{@{}l}
approxRational\ ::\ RealFrac\ a\ =>\ a\ ->\ a\ ->\ Rational
\end{tabular}]\haddockbegindoc
\haddockid{approxRational}, applied to two real fractional numbers \haddocktt{x} and \haddocktt{epsilon},
 returns the simplest rational number within \haddocktt{epsilon} of \haddocktt{x}.
 A rational number \haddocktt{y} is said to be \emph{simpler} than another \haddocktt{y'} if
\par
\begin{itemize}
\item
 \haddocktt{abs\ (numerator\ y)\ <=\ abs\ (numerator\ y')}, and
\par

\item
 \haddocktt{denominator\ y\ <=\ denominator\ y'}.
\par

\end{itemize}
Any real interval contains a unique simplest rational;
 in particular, note that \haddocktt{0/1} is the simplest rational of all.
\par

\end{haddockdesc}
\section{Specification
}
\begin{quote}
{\haddockverb\begin{verbatim}
 module  Data.Ratio (
     Ratio, Rational, (%), numerator, denominator, approxRational ) where
 
 infixl 7  %
 
 ratPrec = 7 :: Int
 
 data  (Integral a)      => Ratio a = !a :% !a  deriving (Eq)
 type  Rational          =  Ratio Integer
 
 (%)                     :: (Integral a) => a -> a -> Ratio a
 numerator, denominator  :: (Integral a) => Ratio a -> a
 approxRational          :: (RealFrac a) => a -> a -> Rational
 
 
 -- "reduce" is a subsidiary function used only in this module.
 -- It normalises a ratio by dividing both numerator
 -- and denominator by their greatest common divisor.
 --
 -- E.g., 12 `reduce` 8    ==  3 :%   2
 --       12 `reduce` (-8) ==  3 :% (-2)
 
 reduce _ 0              =  error "Data.Ratio.% : zero denominator"
 reduce x y              =  (x `quot` d) :% (y `quot` d)
                            where d = gcd x y
 
 x % y                   =  reduce (x * signum y) (abs y)
 
 numerator (x :% _)      =  x
 
 denominator (_ :% y)    =  y
 
 
 instance  (Integral a)  => Ord (Ratio a)  where
     (x:%y) <= (x':%y')  =  x * y' <= x' * y
     (x:%y) <  (x':%y')  =  x * y' <  x' * y
 
 instance  (Integral a)  => Num (Ratio a)  where
     (x:%y) + (x':%y')   =  reduce (x*y' + x'*y) (y*y')
     (x:%y) * (x':%y')   =  reduce (x * x') (y * y')
     negate (x:%y)       =  (-x) :% y
     abs (x:%y)          =  abs x :% y
     signum (x:%y)       =  signum x :% 1
     fromInteger x       =  fromInteger x :% 1
 
 instance  (Integral a)  => Real (Ratio a)  where
     toRational (x:%y)   =  toInteger x :% toInteger y
 
 instance  (Integral a)  => Fractional (Ratio a)  where
     (x:%y) / (x':%y')   =  (x*y') % (y*x')
     recip (x:%y)        =  y % x
     fromRational (x:%y) =  fromInteger x :% fromInteger y
 
 instance  (Integral a)  => RealFrac (Ratio a)  where
     properFraction (x:%y) = (fromIntegral q, r:%y)
                             where (q,r) = quotRem x y
 
 instance  (Integral a)  => Enum (Ratio a)  where
     succ x           =  x+1
     pred x           =  x-1
     toEnum           =  fromIntegral
     fromEnum         =  fromInteger . truncate        -- May overflow
     enumFrom         =  numericEnumFrom               -- These numericEnumXXX functions
     enumFromThen     =  numericEnumFromThen   -- are as defined in Prelude.hs
     enumFromTo       =  numericEnumFromTo     -- but not exported from it!
     enumFromThenTo   =  numericEnumFromThenTo
 
 instance  (Read a, Integral a)  => Read (Ratio a)  where
     readsPrec p  =  readParen (p > ratPrec)
                               (\r -> [(x%y,u) | (x,s)   <- readsPrec (ratPrec+1) r,
                                                 ("%",t) <- lex s,
                                                 (y,u)   <- readsPrec (ratPrec+1) t ])
 
 instance  (Integral a)  => Show (Ratio a)  where
     showsPrec p (x:%y)  =  showParen (p > ratPrec)
                               showsPrec (ratPrec+1) x . 
                               showString " % " . 
                               showsPrec (ratPrec+1) y)
 
 
 
 approxRational x eps    =  simplest (x-eps) (x+eps)
         where simplest x y | y < x      =  simplest y x
                            | x == y     =  xr
                            | x > 0      =  simplest' n d n' d'
                            | y < 0      =  - simplest' (-n') d' (-n) d
                            | otherwise  =  0 :% 1
                                         where xr@(n:%d) = toRational x
                                               (n':%d')  = toRational y
 
               simplest' n d n' d'       -- assumes 0 < n%d < n'%d'
                         | r == 0     =  q :% 1
                         | q /= q'    =  (q+1) :% 1
                         | otherwise  =  (q*n''+d'') :% n''
                                      where (q,r)      =  quotRem n d
                                            (q',r')    =  quotRem n' d'
                                            (n'':%d'') =  simplest' d' r' d r
\end{verbatim}}
\end{quote}
